# Maintainer: Huang-Huang Bao <eh5@eh5.me>

_commit=2cafa8de47fe6f4356b77b83e131fbb3a92683ed

_pkgname=pwconf-merge
_pkgver=0.1.0-pre2

pkgbase=${_pkgname}
pkgname=(${_pkgname} ${_pkgname}-hook)
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="A tool to merge PipeWire configuration"
url="https://github.com/EHfive/pwconf-merge"
license=(MIT)
arch=(any)
makedepends=(npm)
source=("https://registry.npmjs.org/$_pkgname/-/${_pkgname}-${_pkgver}.tgz"
        pwconf-merge.hook)
noextract=("${_pkgname}-${_pkgver}.tgz")
sha256sums=('e166fd07dcbfa3ea3b3fc9a7c3fed7f5d9dacc315e825f00f4572bee375dc14f'
            'a1ffb530b4de99d95449fd2be4f9770535ff5f4c1f9e0c790914a3c3784bfd45')


package_pwconf-merge() {
  depends=(nodejs)
  optdepends=('pwconf-merge-hook: ALPM hook')
  npm install -g  \
    --prefix "${pkgdir}/usr" \
    --cache "${srcdir}/npm-cache" \
    "${_pkgname}-${_pkgver}.tgz"

  install -Dt "${pkgdir}/usr/share/licenses/${_pkgname}" \
    -m644 "${pkgdir}/usr/lib/node_modules/${_pkgname}/LICENSE"

  chown -R root:root "${pkgdir}"
}

package_pwconf-merge-hook() {
  pkgdesc+=" - ALPM hook"
  install -Dt "${pkgdir}/usr/share/libalpm/hooks" -m644 pwconf-merge.hook
}
